<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="PRG_VALVES" Id="{03dcac03-cfad-4254-831b-855af8503ea4}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM PRG_VALVES
VAR
    //Rough lines VRC external interlocks
    bROUGH1_VRC_01_ILK : BOOL;
    bROUGH2_VRC_01_ILK : BOOL;
    bROUGH2_VRC_02_ILK : BOOL;
    bROUGH1_VRC_01_Override : BOOL;
    bROUGH2_VRC_01_Override : BOOL;
    bROUGH2_VRC_02_Override : BOOL;
    bCAT1_VRC_01_ExtILK : BOOL;
    bDP1_VRC_01_ILK : BOOL;
    bDP1_VRC_02_ILK : BOOL;
    bCAT1_VRC_01_ExtILK_US:BOOL;
    bCAT1_VRC_01_ExtILK_DS:BOOL;
    bCAT1_DisableDiffIlk : BOOL;

    //dummy FF output to use in VGC_2S function
    dummy_FFFO : FB_HardwareFFOutput;
    dummy_fbArbiter1 : FB_Arbiter(1);
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(**Gas Injection Valves**)
DREAM_DP1_VRC_01(i_stUSG := DREAM_NC_GPI_01.PG,
                 i_stDSG := DREAM_DP1_GPI_01.PG,
                 i_xDis_DPIlk := FALSE,
                 i_xEPS_OK := TRUE,
                 i_xPMPS_OK := TRUE,
                 i_xReset := FALSE,
                 i_xExt_OK := TRUE,
                 i_xOverrideMode := bSystemOverrideMode,
                 io_fbFFHWO := dummy_FFFO,
                 fbArbiter := dummy_fbArbiter1,
                 i_sDevName := 'DREAM-DP1-VRC-01');

//DP1_VRC_02 Interlock evaluation NOTE: Normally OPEN Valve!!
bDP1_VRC_02_ILK := DREAM_DP1_VRC_01.iq_stValve.i_xOpnLS; //Interlock changed for this valve to only gets interlocked by DREAM_DP1_VRC_01 valve

DREAM_DP1_VRC_02(i_xExtILK_OK := bDP1_VRC_02_ILK, i_xOverrideMode := bSystemOverrideMode); //Normally open valve

(**Catcher valve**)

//CAT1_VRC_01 Interlock evaluation
//Select US interlock gauge and adding pump running interlock when use Pirani
IF NOT stDREAM_MC_GHC_GAUGE.xPRESS_OK THEN
    bCAT1_VRC_01_ExtILK_US := bMCPumpsRunning;
//This interlock pseudo gauge is also assigned in the PRG_Gauge, it is redanduncy and make sure change the PRG_Gauge as well if change here
    stDREAM_MC_GAUGE := DREAM_MC_GPI_01.PG;
    stDREAM_MC_GAUGE.sPath := 'DREAM_MC_GPI_01';
ELSE
    bCAT1_VRC_01_ExtILK_US := True;
//This interlock pseudo MC gauge is also assigned in the PRG_Gauge, it is redanduncy and make sure change the PRG_Gauge as well if change here
    stDREAM_MC_GAUGE := stDREAM_MC_GHC_GAUGE;
    stDREAM_MC_GAUGE.sPath := 'DREAM_MC_GHC_GAUGE';
END_IF

//Select DS interlock gauge
IF NOT DREAM_CAT1_GHC_01.IG.xPRESS_OK THEN
    stDREAM_CAT1_GAUGE := DREAM_ROUGH2_GPI_03.PG;
    stDREAM_CAT1_GAUGE.sPath := 'DREAM_ROUGH2_GPI_03';
ELSE
      stDREAM_CAT1_GAUGE := DREAM_CAT1_GHC_01.IG;
    stDREAM_CAT1_GAUGE.sPath := 'DREAM_CAT1_GHC_01';
END_IF

bCAT1_VRC_01_ExtILK := bCAT1_VRC_01_ExtILK_US AND bCATPumpsRunning;

//DREAM_CAT1_VRC_01(i_xExtILK_OK := bCAT1_VRC_01_ILK, i_xOverrideMode := bSystemOverrideMode);
DREAM_CAT1_VRC_01(i_stUSG := stDREAM_MC_GAUGE,
                  i_stDSG := stDREAM_CAT1_GAUGE,
                  i_xDis_DPIlk := FALSE,
                  i_xEPS_OK := TRUE,
                  i_xPMPS_OK := TRUE,
                  i_xReset := FALSE,
                  i_xExt_OK := bCAT1_VRC_01_ExtILK,
                  i_xOverrideMode := bSystemOverrideMode,
                  io_fbFFHWO := dummy_FFFO,
                  fbArbiter := dummy_fbArbiter1,
                  i_sDevName := 'DREAM-CAT1-VRC-01');

(**Roughing Valves**)
//Interlock evaluation
bROUGH1_VRC_01_ILK := //DREAM_ROUGH1_GPI_01.PG.rPRESS < 5E-3 AND DREAM_ROUGH1_GPI_01.PG.xPRESS_OK AND
                      DREAM_ROUGH1_VFV_01.iq_stVFN.rReqPosition = 0.0 AND
                      DREAM_ROUGH1_VFV_02.iq_stVFN.rReqPosition = 0.0 AND
                      DREAM_ROUGH1_VRC_10.iq_stValve.i_xClsLS; //Interlocked with manifold roughing valve as well

bROUGH2_VRC_01_ILK := //DREAM_ROUGH2_GPI_01.PG.rPRESS < 5E-3 AND DREAM_ROUGH2_GPI_01.PG.xPRESS_OK AND
                      DREAM_ROUGH2_VFV_01.iq_stVFN.rReqPosition = 0.0 AND
                      DREAM_ROUGH2_VFV_02.iq_stVFN.rReqPosition = 0.0;

bROUGH2_VRC_02_ILK := //DREAM_ROUGH2_GPI_01.PG.rPRESS < 5E-3 AND DREAM_ROUGH2_GPI_01.PG.xPRESS_OK AND
                      DREAM_ROUGH2_VFV_01.iq_stVFN.rReqPosition = 0.0 AND
                      DREAM_ROUGH2_VFV_03.iq_stVFN.rReqPosition = 0.0;

bROUGH1_VRC_01_Override := bSystemOverrideMode AND
                           DREAM_ROUGH1_VFV_01.iq_stVFN.rReqPosition = 0.0 AND
                           DREAM_ROUGH1_VFV_02.iq_stVFN.rReqPosition = 0.0;

bROUGH2_VRC_01_Override := bSystemOverrideMode AND
                           DREAM_ROUGH2_VFV_01.iq_stVFN.rReqPosition = 0.0 AND
                           DREAM_ROUGH2_VFV_02.iq_stVFN.rReqPosition = 0.0;

bROUGH2_VRC_02_Override := bSystemOverrideMode AND
                           DREAM_ROUGH2_VFV_01.iq_stVFN.rReqPosition = 0.0 AND
                           DREAM_ROUGH2_VFV_03.iq_stVFN.rReqPosition = 0.0;

DREAM_ROUGH1_VRC_01(i_stUSG := DREAM_ROUGH1_GPI_01.PG,
                    i_stDSG := DREAM_ROUGH1_GPI_02.PG,
                    i_xDis_DPIlk := FALSE,
                    i_xEPS_OK := TRUE,
                    i_xPMPS_OK := TRUE,
                    i_xReset := FALSE,
                    i_xExt_OK := bROUGH1_VRC_01_ILK,
                    i_xOverrideMode := bROUGH1_VRC_01_Override,
                    io_fbFFHWO := dummy_FFFO,
                    fbArbiter := dummy_fbArbiter1,
                    i_sDevName := 'DREAM-ROUGH1-VRC-01');

DREAM_ROUGH2_VRC_01(i_stUSG := DREAM_ROUGH2_GPI_01.PG,
                    i_stDSG := DREAM_ROUGH2_GPI_02.PG,
                    i_xDis_DPIlk := FALSE,
                    i_xEPS_OK := TRUE,
                    i_xPMPS_OK := TRUE,
                    i_xReset := FALSE,
                    i_xExt_OK := bROUGH2_VRC_01_ILK,
                    i_xOverrideMode := bROUGH2_VRC_01_Override,
                    io_fbFFHWO := dummy_FFFO,
                    fbArbiter := dummy_fbArbiter1,
                    i_sDevName := 'DREAM-ROUGH2-VRC-01');

DREAM_ROUGH2_VRC_02(i_stUSG := DREAM_ROUGH2_GPI_01.PG,
                    i_stDSG := DREAM_ROUGH2_GPI_03.PG,
                    i_xDis_DPIlk := FALSE,
                    i_xEPS_OK := TRUE,
                    i_xPMPS_OK := TRUE,
                    i_xReset := FALSE,
                    i_xExt_OK := bROUGH2_VRC_02_ILK,
                    i_xOverrideMode := bROUGH2_VRC_02_Override,
                    io_fbFFHWO := dummy_FFFO,
                    fbArbiter := dummy_fbArbiter1,
                    i_sDevName := 'DREAM-ROUGH2-VRC-02');

//Test valve
DREAM_ROUGH1_VRC_20(i_xExtILK_OK := True, i_xOverrideMode := bSystemOverrideMode);

//Manifold roughing valve
DREAM_ROUGH1_VRC_10(i_xExtILK_OK := DREAM_ROUGH1_VRC_01.iq_stValve.i_xClsLS, i_xOverrideMode := bSystemOverrideMode);

//Flow control valves
DREAM_ROUGH1_VFV_01(i_xExtIlkOK := DREAM_ROUGH1_VRC_01.iq_stValve.i_xClsLS);

DREAM_ROUGH1_VFV_02(i_xExtIlkOK := DREAM_ROUGH1_VRC_01.iq_stValve.i_xClsLS AND DREAM_DP1_VRC_01.iq_stValve.i_xClsLS AND
                                   NOT DREAM_NC_PTM_01.iq_stPTM.i_xAtSpd AND
                                   NOT DREAM_NC_PTM_02.iq_stPTM.i_xAtSpd);

DREAM_ROUGH2_VFV_01(i_xExtIlkOK := DREAM_ROUGH2_VRC_01.iq_stValve.i_xClsLS AND DREAM_ROUGH2_VRC_02.iq_stValve.i_xClsLS);

DREAM_ROUGH2_VFV_02(i_xExtIlkOK := DREAM_ROUGH2_VRC_01.iq_stValve.i_xClsLS AND
                                   NOT DREAM_DP1_PTM_01.iq_stPTM.i_xAtSpd AND
                                   NOT DREAM_DP2_PTM_01.iq_stPTM.i_xAtSpd AND
                                   NOT DREAM_DP3_PTM_01.iq_stPTM.i_xAtSpd AND
                                   NOT DREAM_MC_PTM_01.iq_stPTM.i_xAtSpd AND
                                   NOT DREAM_MC_PTM_02.iq_stPTM.i_xAtSpd AND
                                   NOT DREAM_ROUGH2_PTM_01.iq_stPTM.i_xAtSpd AND
                                   NOT DREAM_ROUGH2_PTM_02.iq_stPTM.i_xAtSpd AND
                                   NOT DREAM_CAT1_PTM_01.iq_stPTM.i_xAtSpd);

DREAM_ROUGH2_VFV_03(i_xExtIlkOK := DREAM_ROUGH2_VRC_02.iq_stValve.i_xClsLS AND DREAM_CAT1_VRC_01.iq_stValve.i_xClsLS AND
                                   NOT DREAM_CAT2_PTM_01.iq_stPtm.i_xAtSpd);

//dual vale for test-08-15-25
DREAM_DP1_VRC_20(i_OPN_OK := TRUE,
                 i_CLS_OK := TRUE,
                 ibCntrlHold := FALSE,
                 i_xOverrideMode :=  bSystemOverrideMode,
                 iq_stValve =>);
                   ]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>